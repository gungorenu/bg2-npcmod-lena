/////////////////////////
//:.  Fifth Scripts  .://
/////////////////////////

//: can stay for 8 hours most
IF
    Global("EU#LenaFifthTimerStarted","LOCALS",0)
THEN
    RESPONSE #1
        SetGlobal("EU#LenaFifthTimerStarted","LOCALS",1)
        SetGlobalTimer("EU#LenaFifthTimer","GLOBAL",2400)  // set timer to 8hours (40minutes)
END

//: Lena called fifth, unsummon him
IF 
    Global("EU#LenaCalledFifth","GLOBAL",1)
THEN
    RESPONSE #1
        SetGlobal("EU#LenaCalledFifth","GLOBAL",0)
        SetGlobal("EU#LenaSpawnedFifth","GLOBAL",0)
        SetGlobalTimer("EU#LenaFifthRestTimer","GLOBAL",300)  // set timer to 1hours (5minutes) to let him rest
        SetGlobalTimer("EU#LenaFifthTimer","GLOBAL",3)  // reset spawn timer for 3sec, it is fine because if it is expired then there is nothing to do fifth is destroyed already
        ActionOverride("EU#LENA",CreateItem("EU#G005",0,0,0))
        DestroySelf()
END

//: timer expired, still alive? then un-summon
IF
    GlobalTimerExpired("EU#LenaFifthTimer","GLOBAL")
    Global("EU#LenaSpawnedFifth","GLOBAL",1)
THEN
    RESPONSE #1
        DisplayStringHeadDead(Myself, @4703)
        Wait(1)
        SetGlobal("EU#LenaSpawnedFifth","GLOBAL",0)
        SetGlobalTimer("EU#LenaFifthRestTimer","GLOBAL",300)  // set timer to 1hours (5minutes) to let him rest
        ActionOverride("EU#LENA",CreateItem("EU#G005",0,0,0))
        DestroySelf()
END

//: fifth died, should not spawn again
IF
    !GlobalTimerExpired("EU#LenaFifthTimer","GLOBAL")
    Die()
THEN
    RESPONSE #1
        SetGlobal("EU#LenaFifthDead","GLOBAL",1)
        DestroySelf()
END
    
//: fifth & cespenar banter
IF
    Global("EU#LenaFifthCespenarBanter","GLOBAL",1)
    See("BOTSMITH")
    !StateCheck("EU#LENA",CD_STATE_NOTVALID)
    !StateCheck("EU#LEFFT",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("BOTSMITH",CD_STATE_NOTVALID)
THEN
    RESPONSE #1
        SetGlobal("EU#LenaFifthCespenarBanter","GLOBAL",2) // fifth will initiate this banter by himself
        RealSetGlobalTimer("EU#LenaFifthTalkTimer","GLOBAL",60) // very short, 1min
        StartDialogueNoSet("BOTSMITH") // Interact() does not work because fifth is not a party NPC
END

//: fifth & player banter
IF
    RealGlobalTimerExpired("EU#LenaFifthTalkTimer","GLOBAL")
    GlobalGT("EU#LenaFifthEnchantBanter","GLOBAL",1)
    GlobalGT("EU#LenaFifthCespenarBanter","GLOBAL",1)
    Global("EU#LenaFifthPlayerBanter","GLOBAL",0)
    !See([ENEMY])
    !StateCheck("EU#LENA",CD_STATE_NOTVALID)
    !StateCheck("EU#LEFFT",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    See("EU#LENA")
    See(Player1)
THEN
    RESPONSE #1
        SetGlobal("EU#LenaFifthPlayerBanter","GLOBAL",1)
        StartDialogueNoSet(Player1) // Interact() does not work because fifth is not a party NPC
END
